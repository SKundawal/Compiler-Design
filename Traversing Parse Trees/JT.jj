options {
  JAVA_UNICODE_ESCAPE = true;
}

PARSER_BEGIN(JTLang)
  public class JTLang {}
PARSER_END(JTLang)


SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LSQPAREN: "[" >
| < RSQPAREN: "]" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < SEMICOLON: ";" >
| < DOT: "." >
| < ASSIGN: "=" >
| < NE: "!=" >
| < PLUS: "+" >
| < AND : "&&" >
| < CLASS: "class" >
| < ELSE: "else" >
| < FALSE: "false" >
| < IF: "if" >
| < WHILE: "while" >
| < INTEGER: "int" >
| < BOOLEAN: "boolean" >
| < MAIN: "main" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < STATIC: "static" >
| < STRING: "String" >
| < THIS: "this" >
| < TRUE: "true" >
| < PRINT: "System.out.println" >
| < LENGTH: "length" >
| < VOID: "void" >
| < EXTENDS: "extends" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL: ( ["1"-"9"] (["0"-"9"])* | "0" ) >
|
  < DOUBLE_LITERAL: ( <INTEGER_LITERAL> <DOT> (["0"-"9"])* )>
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}

/*************************************
 * The JTLang Grammar Starts Here *
 *************************************/

void Goal() :
{}
{
  MainClass()
  ( TypeDeclaration() )*
  <EOF>
}

void MainClass() :
{}
{
  <CLASS> Identifier() <LBRACE>
    <PUBLIC> <STATIC> <VOID> <MAIN> <LPAREN> <STRING> <LSQPAREN> <RSQPAREN> Identifier() <RPAREN>
    <LBRACE> PrintStatement() <RBRACE>
  <RBRACE>
}

void TypeDeclaration() :
{}
{
  LOOKAHEAD(3) ClassDeclaration()
  /** extends implementation */
  | ExtendsDeclaration()
}

void ExtendsDeclaration() :
{}
{
  <CLASS> Identifier() <EXTENDS>  Identifier() <LBRACE>
                                                  ( MethodDeclaration() )*
                                              <RBRACE>
}

void ClassDeclaration() :
{}
{
  "class" Identifier() "{"
                           ( MethodDeclaration() )*
                       "}"
}

void VarDeclaration() :
{}
{
  Type() Identifier() ";"
}

void MethodDeclaration() :
{}
{
  "public" Type() Identifier()
  "(" ( FormalParameterList() )? ")"
  "{" ( LOOKAHEAD(2) VarDeclaration() )*
      ( Statement() )* "return" Expression() ";" "}"
}

void FormalParameterList() :
{}
{
  FormalParameter() ( FormalParameterRest() )*
}

void FormalParameter() :
{}
{
  Type() Identifier()
}

void FormalParameterRest() :
{}
{
  "," FormalParameter()
}

void Type() :
{}
{
  LOOKAHEAD(3)
  ArrayType()
|
  IntegerType()
|
  BooleanType()
|
  Identifier()
|
  DoubleType()
}

void ArrayType() :
{}
{
  "int" "[" "]"
}

void IntegerType() :
{}
{
  "int"
}

void BooleanType() :
{}
{
  "boolean"
}

void DoubleType() :
{}
{
  "double"
}

void Statement() :
{}
{
  Block()
|
  LOOKAHEAD(2)
  AssignmentStatement()
|
  LOOKAHEAD(2)
  ArrayAssignmentStatement()
|
  IfStatement()
|
  WhileStatement()
|
  PrintStatement()
}

void Block() :
{}
{
  "{" ( Statement() )* "}"
}

void AssignmentStatement() :
{}
{
  Identifier() "=" Expression() ";"
}

void ArrayAssignmentStatement() :
{}
{
  Identifier() "[" Expression() "]" "=" Expression() ";"
}

void IfStatement() :
{}
{
  // Add if-then stmt as well
  LOOKAHEAD(IfthenElseStatement()) IfthenElseStatement()
  | IfthenStatement()
}
void IfthenStatement() :
{}
{
  "if" "(" Expression() ")" Statement()
}
void IfthenElseStatement() :
{}
{
  "if" "(" Expression() ")" Statement() "else" Statement()
}
void WhileStatement() :
{}
{
  "while" "(" Expression() ")" Statement()
}

void PrintStatement() :
{}
{
  "System.out.println" "(" Expression() ")" ";"
}

void Expression() :
{}
{
  LOOKAHEAD( PrimaryExpression() "||" )
  OrExpression()
|
  LOOKAHEAD( PrimaryExpression() "+" )
  AddExpression()
|
  LOOKAHEAD( PrimaryExpression() "*" )
  MulExpression()
|
  LOOKAHEAD( PrimaryExpression() "!=" )
  NEQExpression()
|
  LOOKAHEAD( PrimaryExpression() "&&" )
  AndExpression()
|
  LOOKAHEAD( PrimaryExpression() "==" )
  EqExpression()
|
  LOOKAHEAD( PrimaryExpression() "-" )
  SubExpression()
|
   LOOKAHEAD( PrimaryExpression() "." Identifier() "(" )
   MessageSend()
|
   LOOKAHEAD(3) PrimaryExpression()
}

void OrExpression() :
{}
{
  PrimaryExpression() "||" PrimaryExpression()
}

void AndExpression() :
{}
{
  PrimaryExpression() "&&" PrimaryExpression()
}

void AddExpression() :
{}
{
 PrimaryExpression() "+" PrimaryExpression()
}

void MulExpression() :
{}
{
 PrimaryExpression() "*" PrimaryExpression()
}

void EqExpression() :
{}
{
  PrimaryExpression() "==" PrimaryExpression()
}

void NEQExpression() :
{}
{
  PrimaryExpression() "!=" PrimaryExpression()
}

void SubExpression() :
{}
{
  PrimaryExpression() "-" PrimaryExpression()
}

void MessageSend() :
{}
{
  PrimaryExpression() "." Identifier()
  "(" ( ExpressionList() )? ")"
}
void ExpressionList() :
{}
{
  Expression() ( ExpressionRest() )*
}

void ExpressionRest() :
{}
{
  "," Expression()
}

void PrimaryExpression() :
{}
{
  IntegerLiteral()
|
  TrueLiteral()
|
  FalseLiteral()
|
  DoubleLiteral()
|
  LOOKAHEAD(2) ArrayLengthExpression()
|
  LOOKAHEAD(2) ArrayLookUpExpression()
|
  Identifier()
|
  ThisExpression()
|
  LOOKAHEAD(3) ArrayAllocationExpression()
|
  AllocationExpression()
|
  NotExpression()
|
  BracketExpression()
}

void IntegerLiteral() :
{}
{
  <INTEGER_LITERAL>
}

void TrueLiteral() :
{}
{
  "true"
}

void FalseLiteral() :
{}
{
  "false"
}

void DoubleLiteral() :
{}
{
  <DOUBLE_LITERAL>
}

void Identifier() :
{}
{
  <IDENTIFIER>
}

void ThisExpression() :
{}
{
  "this"
}

void ArrayLengthExpression() :
{}
{
  Identifier() <DOT> <LENGTH>
}

void AllocationExpression() :
{}
{
  "new" Identifier() "(" ")"
}

void ArrayLookUpExpression() :
{}
{
  Identifier() <LSQPAREN> Identifier() <RSQPAREN>
}

void NotExpression() :
{}
{
  "!" Expression()
}

void BracketExpression() :
{}
{
  "(" Expression() ")"
}

void ArrayAllocationExpression() :
{}
{
  "new" "int" "[" Expression() "]"
}

void IdentifierList() :
{}
{
  Identifier() ( IdentifierRest() )*
}

void IdentifierRest() :
{}
{
  "," Identifier()
}

